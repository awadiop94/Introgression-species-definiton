# Author: Awa Diop adiop2@ncsu.edu
# Author: Louis-Marie Bobay ljbobay@ncsu.edu

# Snakefile_stage2
import yaml

def get_ref_dirnames():
    with open("configref.yaml") as f:
        cfg = yaml.safe_load(f)
    return cfg.get("dirname", [])

# Function to extract the reference genome for each {ref}
def get_ref_genome(wildcards):
    path = f"GENOMES/Gene_Flow/ReferenceDatabase/{wildcards.ref}/path_to_genome_list.txt"
    with open(path, "r") as g:
        genome = g.read()
    return genome.split('\n', 1)[0]   # first line only


# =============================
# TARGETS
# =============================
rule all:
    input:
        "configcand.yaml"
        

# ==========================================================
# GENE FLOW ANALYSIS - BUILD QUERY-DATABASE
# ==========================================================

rule core_genome_within_species:
    input:
        dir="GENOMES/genomes",
        conf="configref.yaml"
    output:
        "GENOMES/Gene_Flow/ReferenceDatabase/{ref}/core_genome/concat.fa",
        "GENOMES/Gene_Flow/ReferenceDatabase/{ref}/core_genome/families_core.txt"
    params:
        liste="GENOMES/Gene_Flow/ReferenceDatabase/{ref}/path_to_genome_list.txt",
        dir="GENOMES/Gene_Flow/ReferenceDatabase/{ref}/core_genome",
        REF=get_ref_genome   # dynamically computed from {ref}
    threads: 8
    resources: mem_mb=32000
    shell:
        "python pipelines/CoreCruncher/corecruncher_master.py "
        "-in {input.dir} -out {params.dir} -list {params.liste} -ref {params.REF} "
        "-freq 85 -prog usearch -ext .fa -length 80 -score 70 -align mafft"


rule gene_flow_refsp:
    input:
        fasta="GENOMES/Gene_Flow/ReferenceDatabase/{ref}/core_genome/concat.fa"
    output:
        done_flag="GENOMES/Gene_Flow/ReferenceDatabase/{ref}/core_genome/for_removal.txt"
    params:
        results_dir="GENOMES/Gene_Flow/ReferenceDatabase/{ref}/"
    threads: 8
    resources: mem_mb=32000
    shell:
        """
        python pipelines/ConSpecifix/Gene_Flow_within_Refsp/analyze_gene_flow_refsp.py {params.results_dir}
        touch {output.done_flag}
        """


rule build_QueryDatabase:
    input:
        cluster="GENOMES/ANI_results/genome_clusters.csv",
        removal=expand("GENOMES/Gene_Flow/ReferenceDatabase/{ref}/core_genome/for_removal.txt",
                       ref=get_ref_dirnames())
    output:
        conf="configcand.yaml"
    params:
        dir="GENOMES"
    threads: 1
    resources: mem_mb=4000
    shell:
        """
        mkdir -p GENOMES/Gene_Flow/QueryDatabase
        python pipelines/ConSpecifix/create_Querydb.py {params.dir}
        """
